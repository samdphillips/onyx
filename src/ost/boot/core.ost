
nil subclass: Object [

    == anObject [
        self _objectEqual: anObject
    ]

    = anObject   [ self == anObject ]
    ~= anObject  [ (self = anObject) not ]
    ~~ anObject [ (self == anObject) not ]

    equalToSmallInt: aSmallInt [ false ]

    class [
        self _objectClass
    ]

    stackTrace [
        self _objectStackTrace
    ]

    debug [
        self _objectDebug
    ]

    error: aString [
        Error new
            receiver: self;
            messageText: aString;
            signal
    ]

    doesNotUnderstand: aMessage [
        MessageNotUnderstood new
            receiver: self;
            message: aMessage;
            signal
    ]

    halt [
        self _objectHalt
    ]

    isKindOf: aClass [
        | cls |
        cls := self class.
        [
            cls isNil ifTrue: [ ^ false ].
            cls == aClass ifTrue: [ ^ true ].
            cls := cls superclass
        ] repeat
    ]

    isMemberOf: aClass [
       self class == aClass
    ]

    yourself [
        self
    ]

    asString [
        self printString
    ]

    printString [
        | clsName article |
        clsName := self class name asString.
        article :=
            (Character vowels includes: (clsName at: 0) asLowercase)
                ifTrue: [ 'an' ] ifFalse: [ 'a' ].

        article, ' ', clsName, ' object'
    ]

    print [
        self printString display
    ]

    printNl [
        self print.
        Character newline asString print
    ]

    -> anObject [
        Association key: self value: anObject
    ]

    ifNil: aBlock [
        nil
    ]

    ifNotNil: aBlock [
        aBlock value
    ]

    ifNil: nilBlock ifNotNil: notNilBlock [
        notNilBlock value
    ]

    ifNotNil: notNilBlock ifNil: nilBlock [
        self ifNil: nilBlock ifNotNil: notNilBlock
    ]

    isNil          [ false ]
    isNotNil       [ true  ]
    isClass        [ false ]
    isTrait        [ false ]
    isBoolean      [ false ]
    isNumber       [ false ]
    isSmallInt     [ false ]
    isArray        [ false ]
    isString       [ false ]
    isSymbol       [ false ]
    isContinuation [ false ]
]

Object subclass: Class [
    new [
        self _classNew
    ]

    superclass [
        self _classSuperclass
    ]

    name [
        self _className
    ]

    printString [
        'the ', self name asString, ' class'
    ]
]

Object subclass: Trait [
    Trait class [
        new [
            self _traitNew
        ]

        build: aTrait [
            aTrait asTrait
        ]
    ]

    asTrait [ self ]

    isTrait [ true ]

    copy [
        Trait new
            merge: self;
            yourself
    ]

    + aTrait [
        Trait new
            merge: self;
            merge: aTrait asTrait;
            yourself
    ]

    - aCollection [
        | trait |
        trait := self copy.
        aCollection do: [:selector |
            trait remove: selector
        ].
        trait
    ]

    @ aCollection [
        | trait oldSelectors newSelectors |
        trait := self copy.
        oldSelectors := OrderedCollection new: aCollection size.
        newSelectors := OrderedCollection new: aCollection size.

        aCollection do: [:assoc |
            oldSelectors add: assoc key.
            newSelectors add: assoc value
        ].
        self rename: oldSelectors to: newSelectors.

        trait
    ]

    merge: aTrait [
        self _traitMerge: aTrait asTrait
    ]

    rename: srcSelectors to: destSelectors [
        self _traitRename: srcSelectors asArray to: destSelectors asArray
    ]

    remove: aSelector [
        self _traitRemove: aSelector
    ]

    validateUse: aClass [
        self _traitValidateUse: aClass
    ]
]

Trait named: THashable [
    isHashable [ true ]

    hash [
        | aHasher |
        aHasher := Hasher new.
        self hashBytesOn: aHasher.
        aHasher hashValue
    ]
]

Object subclass: UndefinedObject [
    isNil    [ true  ]
    isNotNil [ false ]

    ifNil: aBlock [
        aBlock value
    ]

    ifNotNil: aBlock [
        nil
    ]

    ifNil: nilBlock ifNotNil: notNilBlock [
        nilBlock value
    ]

    printString [ 'nil' ]
]

Object subclass: Boolean [
    Boolean uses: THashable.

    isBoolean [ true ]

    ifFalse: falseBlock ifTrue: trueBlock [
        self ifTrue: trueBlock ifFalse: falseBlock
    ]

    hashBytesOn: aHasher [
        aHasher nextPut:
            (self
                ifTrue: [ 1 ]
                ifFalse: [ -1 ])
    ]
]

Boolean subclass: True [
    ifTrue: aBlock [
        aBlock value
    ]

    ifFalse: aBlock [
        nil
    ]

    ifTrue: trueBlock ifFalse: falseBlock [
        trueBlock value
    ]

    not [ false ]

    and: aBlock [
        aBlock value
    ]

    or: aBlock [
        true
    ]

    asString [ 'true' ]
]

Boolean subclass: False [
    ifTrue: aBlock [
        nil
    ]

    ifFalse: aBlock [
        aBlock value
    ]

    ifTrue: trueBlock ifFalse: falseBlock [
        falseBlock value
    ]

    not [ true ]

    and: aBlock [
        false
    ]

    or: aBlock [
        aBlock value
    ]

    asString [ 'false' ]
]

Object subclass: Message [
    | selector arguments |

    selector  [ selector  ]
    arguments [ arguments ]
]

Object subclass: BlockClosure [
    argumentCount [ self _blockArgumentCount ]

    value [ self _blockValue ]

    value: a [
        self _blockValue: a
    ]

    value: a value: b [
        self _blockValue: a value: b
    ]

    value: a value: b value: c [
        self _blockValue: a value: b value: c
    ]

    value: a value: b value: c value: d [
        self _blockValue: a value: b value: c value: d
    ]

    valueWithArguments: aCollection [
        self _blockValueWithArguments: aCollection asArray
    ]

    cull: a [
        self argumentCount < 1
            ifTrue: [ self value ]
            ifFalse: [ self value: a ]
    ]

    and: aBlock [
        self value and: aBlock
    ]

    withContinuation [
        self withContinuation: Continuation defaultTag
    ]

    withContinuation: promptTag [
        self _blockWithContinuation: promptTag
    ]

    withPrompt [
        | p |
        p := PromptTag new.
        (self argumentCount = 1
            ifTrue: [ [ self value: p ] ]
            ifFalse: [ self ]) withPrompt: p
    ]

    withPrompt: promptTag [
        self withPrompt: promptTag abort: [:x | x ]
    ]

    withPrompt: promptTag abort: aBlock [
        self _blockWithPrompt: promptTag abort: aBlock
    ]

    withEscape [
        [:p | self value: [:v | p abort: v ] ] withPrompt
    ]

    withFullEscape [
        [:p |
            self value: [:v |
                (CurtailedMark marks: p) do: [:b | b value ].
                p abort: v ]
        ] withPrompt
    ]

    withMark: tag value: anObject [
        self _blockWithMark: tag value: anObject
    ]

    returnTo [
        self _blockReturnTo
    ]

    repeat [
        self value.
        self repeat
    ]

    whileTrue: aBlock [
        [ self value
            ifFalse: [ ^ nil ]
            ifTrue:  [ aBlock value ] ] repeat
    ]

    whileFalse: aBlock [
        [ self value not ] whileTrue: aBlock
    ]

    on: anExceptionSelector do: aBlock [
        | handler |
        handler := ExceptionHandler new
                       selector: anExceptionSelector
                       handlerBlock: aBlock.

        handler installAndDo: self
    ]

    ifCurtailed: aBlock [
        [ self withPrompt ] withMark: CurtailedMark value: aBlock
    ]

]

Object subclass: Hasher [
    "
    Implementation of DJB hashing function
    http://www.partow.net/programming/hashfunctions/#DJBHashFunction
    "
    | state |

    Hasher class [
        new [ super new initialize; yourself ]
    ]

    initialize [
        state := 5381
    ]

    reset [
        self initialize
    ]

    hashValue [
        state
    ]

    nextPut: aByte [
        state :=
            ((((state bitShift: 5) + state)
                bitAnd: 16rFFFF_FFFF_FFFF_FFFF) + aByte)
                    bitAnd: 16rFFFF_FFFF_FFFF_FFFF
    ]
]
